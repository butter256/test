Перем ИзменилосьНаименование;

#Область ПрограммныйИнтерфейс

Функция КлючОбъекта() Экспорт
	Возврат "Имя";
КонецФункции	

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	ИзменилосьНаименование = Наименование <> Ссылка.Наименование;
	
	Тип = ТипПредмета.Получить();
	Если ТипЗнч(Тип) = Тип("ОписаниеТипов") И Тип.Типы().Количество() тогда 
		МетаданныеПредмета = Метаданные.НайтиПоТипу(Тип.Типы()[0]);
		Если МетаданныеПредмета <> Неопределено тогда
			ПолноеИмяМетаданныхПредмета = МетаданныеПредмета.ПолноеИмя();
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если Использовать тогда
		
		Отказ = Не Справочники.мдмБизнесПроцессы.ПроверитьСхему(Ссылка);
		
	КонецЕсли;	
	
	Если Не Отказ И ИзменилосьНаименование тогда
		
		Выборка = Справочники.мдмФормыДанных.Выбрать(, Ссылка);
		Пока Выборка.Следующий() цикл
			Объект = Выборка.ПолучитьОбъект();
			Объект.ПредставлениеСсылки = Наименование + "." + Выборка.Наименование;
			Объект.Записать();
		КонецЦикла;	
		
	КонецЕсли;	
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.мдмСхемыДанных") Тогда
		Наименование = ДанныеЗаполнения.Наименование;
		Имя = ДанныеЗаполнения.Наименование;
		СхемаДанных = ДанныеЗаполнения.Ссылка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
