
&НаКлиенте
Перем ЭтоЗапись;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Карта = ТекущийОбъект.Карта.Получить();
	Тип = ТекущийОбъект.ТипПредмета.Получить();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Тип.ДоступныеТипы = мдмМетаданные.ТипыОбъектовМастерДанных();
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) тогда
		Карта = Справочники.мдмБизнесПроцессы.ПолучитьМакет("ПростойМакетБизнесПроцесса");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭтоЗапись = Ложь;
	
	ЗаполнитьИменаЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ЭтоЗапись = Истина;
	
	ОбновитьИменаЭлементов();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ЭтоНовый() тогда
		БизнесПроцесс = Справочники.мдмБизнесПроцессы.ПолучитьСсылку();
		ТекущийОбъект.УстановитьСсылкуНового(БизнесПроцесс);
	Иначе 
		БизнесПроцесс = ТекущийОбъект.Ссылка;	
	КонецЕсли;	
	
	СохранитьТочкиБизнесПроцесса(БизнесПроцесс);
	
	мдмТочкиБизнесПроцессаФормы.ОбновитьПодсказкиЭлементовКарты(Карта);
	
	ТекущийОбъект.Карта = Новый ХранилищеЗначения(Карта);
	ТекущийОбъект.ТипПредмета = Новый ХранилищеЗначения(Тип);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	ЭтоЗапись = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КартаПриАктивизации(Элемент)
	
	ПодключитьОбработчикОжидания("ОбновитьИменаЭлементов", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КартаПриИзменении(Элемент)
	
	ПодключитьОбработчикОжидания("ОбновитьИменаЭлементов", 0.1, Истина);

КонецПроцедуры

&НаСервере
Процедура КартаПриИзмененииНаСервере()
	
	мдмТочкиБизнесПроцессаФормы.ОбновитьПодсказкиЭлементовКарты(Карта);
	
КонецПроцедуры	

&НаКлиенте
Процедура КартаВыбор(Элемент)
	
	ОткрытьНастройкиТочки();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкиТочки(Команда)
	
	ОткрытьНастройкиТочки();	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИмяФормыОбъекта(Точка)
	
	Возврат Точка.Метаданные().Формы.ФормаЭлемента.ПолноеИмя();
	
КонецФункции	

&НаСервере
Процедура СохранитьТочкиБизнесПроцесса(БизнесПроцесс)
	
	ТаблицаСвязей = Новый ТаблицаЗначений;
	ТаблицаСвязей.Колонки.Добавить("Наименование");
	ТаблицаСвязей.Колонки.Добавить("Начало");
	ТаблицаСвязей.Колонки.Добавить("Конец");
	ТаблицаСвязей.Колонки.Добавить("НачалоВариант");
	ТаблицаСвязей.Колонки.Добавить("КонецВариант");
	
	Точки = Новый Массив;
	
	Для Каждого ЭлементСхемы из Карта.ЭлементыГрафическойСхемы цикл
		
		ТипЭлемента = ТипЗнч(ЭлементСхемы);
		
		Если ТипЭлемента = Тип("ЭлементГрафическойСхемыСоединительнаяЛиния") 
			ИЛИ ТипЭлемента = Тип("ЭлементГрафическойСхемыДекоративнаяЛиния") тогда
			
			ДобавитьСвязь(ЭлементСхемы, ТаблицаСвязей);
			
			Продолжить;
			
		КонецЕсли;
		
		МенеджерТочки = мдмТочкиБизнесПроцессаФормы.МенеджерТочки(ЭлементСхемы);
		
		Точка = МенеджерТочки.ПолучитьПоИмени(ЭлементСхемы.Имя, БизнесПроцесс);
		
		МенеджерТочки.СоздатьОбновитьТочки(Точка, ЭлементСхемы, БизнесПроцесс);
		
		Точки.Добавить(Точка);		
		
	КонецЦикла;	
	
	Для Каждого Точка Из Точки Цикл
		
		МенеджерТочки = мдмТочкиБизнесПроцесса.МенеджерТочки(Точка);	
		
		МенеджерТочки.ОбновитьСвязиТочки(Точка, ТаблицаСвязей);
		
	КонецЦикла;	
	
КонецПроцедуры	

&НаСервере
Процедура ОбновитьТочку(НовоеИмя, СтароеИмя)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) тогда
		Возврат;
	КонецЕсли;	
	
	ЭлементСхемы = Карта.ЭлементыГрафическойСхемы[НовоеИмя];
	
	МенеджерТочки = мдмТочкиБизнесПроцессаФормы.МенеджерТочки(ЭлементСхемы);
	
	Точка = МенеджерТочки.ПолучитьПоИмени(СтароеИмя, Объект.Ссылка);
	
	Если ЗначениеЗаполнено(Точка) тогда
		МенеджерТочки.СоздатьОбновитьТочки(Точка, ЭлементСхемы, Объект.Ссылка);
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура ПометитьТочкуНаУдаление(Имя)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) тогда
		Возврат;
	КонецЕсли;	
	
	Точка = Справочники.мдмТочкиБизнесПроцесса.ПолучитьПоИмени(Имя, Объект.Ссылка);
	
	Если ЗначениеЗаполнено(Точка) тогда
		ТочкаОбъект = Точка.ПолучитьОбъект();
		ТочкаОбъект.УстановитьПометкуУдаления(Истина);
	КонецЕсли;	
	
КонецПроцедуры	

&НаСервере
Процедура ДобавитьСвязь(СоединительнаяЛиния, ТаблицаСвязей)
	
	НоваяСтрока = ТаблицаСвязей.Добавить();
	
	Если СоединительнаяЛиния.НачалоЭлемент <> Неопределено Тогда 
		НоваяСтрока.Начало = СоединительнаяЛиния.НачалоЭлемент.Имя;
	КонецЕсли;	
	
	Если СоединительнаяЛиния.КонецЭлемент <> Неопределено Тогда
		НоваяСтрока.Конец = СоединительнаяЛиния.КонецЭлемент.Имя;
	КонецЕсли;	
	
	Если СоединительнаяЛиния.НачалоВариант <> Неопределено тогда
		НоваяСтрока.НачалоВариант = СоединительнаяЛиния.НачалоВариант.Значение;
	КонецЕсли;
	
	Если СоединительнаяЛиния.КонецВариант <> Неопределено тогда
		НоваяСтрока.КонецВариант = СоединительнаяЛиния.КонецВариант.Значение;
	КонецЕсли;

	НоваяСтрока.Наименование = СоединительнаяЛиния.Наименование;
	
КонецПроцедуры	

&НаСервере
Функция ТочкаБизнесПроцесса()
	
	ЭлементСхемы = Карта.ЭлементыГрафическойСхемы[ИмяТекущегоЭлементаСхемы]; 
	
	МенеджерТочки = мдмТочкиБизнесПроцессаФормы.МенеджерТочки(ЭлементСхемы);
	
	Если МенеджерТочки = Неопределено тогда
		Возврат Неопределено;
	КонецЕсли;	
	
	Возврат МенеджерТочки.ПолучитьПоИмени(ЭлементСхемы.Имя, Объект.Ссылка);
	
КонецФункции	

&НаСервере
Функция ЕстьМенеджерТочки()
	
	Перем МенеджерТочки;
	
	Если ЗначениеЗаполнено(ИмяТекущегоЭлементаСхемы) тогда
		
		ЭлементСхемы = Карта.ЭлементыГрафическойСхемы[ИмяТекущегоЭлементаСхемы]; 
		МенеджерТочки = мдмТочкиБизнесПроцессаФормы.МенеджерТочки(ЭлементСхемы);
		
	КонецЕсли;

	Возврат МенеджерТочки <> Неопределено;
	
КонецФункции	

&НаКлиенте
Процедура ОткрытьНастройкиТочки(ТекущееИмя = Неопределено)
	
	Если ЗначениеЗаполнено(ТекущееИмя) тогда
		
		ИмяТекущегоЭлементаСхемы = ТекущееИмя;	
		
	Иначе 	
		
		ТекущийЭлементСхемы = Элементы.Карта.ТекущийЭлемент;
		
		Если ТекущийЭлементСхемы = Неопределено тогда
			ИмяТекущегоЭлементаСхемы = "";
		Иначе 	
			ИмяТекущегоЭлементаСхемы = ТекущийЭлементСхемы.Имя;
		КонецЕсли;	
		
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) тогда
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ИмяТекущегоЭлементаСхемы", ИмяТекущегоЭлементаСхемы);
		Оповещение = Новый ОписаниеОповещения("ВопросОткрытьНастройкиТочкиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьВопрос(Оповещение, "Для изменения настроек точки нужно записать бизнес-процесс. Продолжить?", РежимДиалогаВопрос.ДаНет);
		Возврат;
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ИмяТекущегоЭлементаСхемы) тогда
		
		Точка = ТочкаБизнесПроцесса();
		
	КонецЕсли;	

	ТочкаНеЗаполнена = Не ЗначениеЗаполнено(Точка);
	
	Если ТочкаНеЗаполнена И ЕстьМенеджерТочки() тогда
		
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", 
			Новый Структура("Имя, Наименование, Владелец",
				ТекущийЭлементСхемы.Имя,
				ТекущийЭлементСхемы.Наименование,
				Объект.Ссылка
			)
		);		
		
	ИначеЕсли ТочкаНеЗаполнена тогда	
		
		Если ЗначениеЗаполнено(ИмяТекущегоЭлементаСхемы) тогда
			ПоказатьПредупреждение(, "Для элемента не предусмотрено настроек.");
		КонецЕсли;
		
		Возврат;
		
	Иначе 
		
		ПараметрыФормы = Новый Структура("Ключ", Точка);
		
	КонецЕсли;	
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяТекущегоЭлементаСхемы", ИмяТекущегоЭлементаСхемы);
	
	Оповещение = Новый ОписаниеОповещения("ОткрытьНастройкиТочкиПриЗавершении", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму(ИмяФормыОбъекта(Точка), ПараметрыФормы, ЭтотОбъект, ,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры	

&НаКлиенте
Процедура ВопросОткрытьНастройкиТочкиЗавершение(Ответ, ДополниельныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да И Записать() тогда
		ОткрытьНастройкиТочки(ДополниельныеПараметры.ИмяТекущегоЭлементаСхемы);
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ОткрытьНастройкиТочкиПриЗавершении(Результат, ДополнительныеПараметры) Экспорт
	
	ОткрытьНастройкиТочкиПриЗавершенииНаСервере(Результат, ДополнительныеПараметры);	
	
КонецПроцедуры	

&НаСервере
Процедура ОткрытьНастройкиТочкиПриЗавершенииНаСервере(Результат, ДополнительныеПараметры) Экспорт
	
	Точка = ТочкаБизнесПроцесса();
	Если ЗначениеЗаполнено(Точка) тогда
		ИмяЭлементаСхемы = ДополнительныеПараметры.ИмяТекущегоЭлементаСхемы;
		
		ЭлементСхемы = Карта.ЭлементыГрафическойСхемы[ИмяЭлементаСхемы];
		ЭлементСхемы.Наименование = Точка.Наименование;
		
		мдмТочкиБизнесПроцессаФормы.ОбновитьПодсказкиЭлементовКарты(Карта);
	КонецЕсли;

КонецПроцедуры	

&НаКлиенте
Процедура ОбновитьИменаЭлементов() Экспорт
	
	Для Каждого СтрокаЭлементов из ИменаЭлементовКарты цикл
		СтрокаЭлементов.НовоеИмя = "";
	КонецЦикла;	
	
	Для Каждого ЭлементСхемы из Карта.ЭлементыГрафическойСхемы цикл
		ТипЭлемента = ТипЗнч(ЭлементСхемы);
		Если ТипЭлемента = Тип("ЭлементГрафическойСхемыСоединительнаяЛиния") 
			ИЛИ ТипЭлемента = Тип("ЭлементГрафическойСхемыДекоративнаяЛиния") тогда
			Продолжить;
		КонецЕсли;
			
		НайденныеСтроки = ИменаЭлементовКарты.НайтиСтроки(Новый Структура("Имя", ЭлементСхемы.Имя));
		Если НайденныеСтроки.Количество() = 0 тогда
			ИменаЭлементовКарты.Добавить().НовоеИмя = ЭлементСхемы.Имя;
		Иначе 
			НайденныеСтроки[0].НовоеИмя = ЭлементСхемы.Имя;	
		КонецЕсли;	
	КонецЦикла;
	
	НовыеИмена = ИменаЭлементовКарты.НайтиСтроки(Новый Структура("Имя", ""));
		
	СтарыеИмена = ИменаЭлементовКарты.НайтиСтроки(Новый Структура("НовоеИмя", ""));
	
	Если НовыеИмена.Количество() И СтарыеИмена.Количество() тогда
		Если ЗначениеЗаполнено(Объект.Ссылка) тогда
			ОбновитьТочку(НовыеИмена[0].НовоеИмя, СтарыеИмена[0].Имя);
		КонецЕсли;
		СтарыеИмена[0].Имя = НовыеИмена[0].НовоеИмя;
		ИменаЭлементовКарты.Удалить(НовыеИмена[0]);
	ИначеЕсли НовыеИмена.Количество() тогда	
		Для Каждого СтрокаНовогоИмени из НовыеИмена цикл
			СтрокаНовогоИмени.Имя = СтрокаНовогоИмени.НовоеИмя;
		КонецЦикла;	
	ИначеЕсли СтарыеИмена.Количество() тогда	
		Для Каждого СтрокаСтарогоИмени из СтарыеИмена цикл
			ПометитьТочкуНаУдаление(СтрокаСтарогоИмени.Имя);
			ИменаЭлементовКарты.Удалить(СтрокаСтарогоИмени);	
		КонецЦикла;	
	КонецЕсли;	
	
	Если Не ЭтоЗапись тогда
		ТекущийЭлемент = Элементы.Карта;
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьИменаЭлементов()
	
	Для Каждого ЭлементСхемы из Карта.ЭлементыГрафическойСхемы цикл
		ТипЭлемента = ТипЗнч(ЭлементСхемы);
		Если ТипЭлемента = Тип("ЭлементГрафическойСхемыСоединительнаяЛиния") 
			ИЛИ ТипЭлемента = Тип("ЭлементГрафическойСхемыДекоративнаяЛиния") тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ИменаЭлементовКарты.Добавить();
		НоваяСтрока.Имя = ЭлементСхемы.Имя;
		НоваяСтрока.НовоеИмя = ЭлементСхемы.Имя;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти