
#Область ПрограммныйИнтерфейс

Функция СоздатьСхемуКомпоновкиДанных(РеквизитыОбъекта, РеквизитыФормы = Неопределено) Экспорт

	СхемаКомпоновкиДанных = Новый СхемаКомпоновкиДанных;

	ИсточникДанных = СхемаКомпоновкиДанных.ИсточникиДанных.Добавить();
	ИсточникДанных.Имя = "Форма";
	ИсточникДанных.ТипИсточникаДанных = "Local";

	НаборДанных = СхемаКомпоновкиДанных.НаборыДанных.Добавить(Тип("НаборДанныхОбъектСхемыКомпоновкиДанных"));
	НаборДанных.Имя = "Форма";
	НаборДанных.ИмяОбъекта = "ДанныеПроцесса";
	НаборДанных.ИсточникДанных = "Форма";
	
	ПоляВыбора = СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Выбор.Элементы;
	
	Для Каждого Реквизит из РеквизитыОбъекта цикл
		
		СвойстваРеквизита = Реквизит.Значение;
		
		ПолеНабора = ДобавитьПолеНабора(НаборДанных, СвойстваРеквизита.Имя, СвойстваРеквизита.Тип, СвойстваРеквизита.Имя, СвойстваРеквизита.Синоним);  
		
		ДобавитьПолеВыбора(ПоляВыбора, ПолеНабора.Поле, ПолеНабора.Заголовок)
		
	КонецЦикла;	
	
	Если РеквизитыФормы <> Неопределено тогда
		Для Каждого Реквизит из РеквизитыФормы цикл
			
			ПолеНабора = ДобавитьПолеНабора(НаборДанных, Реквизит.Имя, Реквизит.Тип, Реквизит.Имя, Реквизит.Синоним);  
			
			ДобавитьПолеВыбора(ПоляВыбора, ПолеНабора.Поле, ПолеНабора.Заголовок)
			
		КонецЦикла;	
	КонецЕсли;

	ДобавитьГруппировкуДетальныеЗаписи(СхемаКомпоновкиДанных.НастройкиПоУмолчанию.Структура);
	
	Возврат СхемаКомпоновкиДанных;
	
КонецФункции	

Функция ПроверитьУсловиеОформления(СхемаКомпоновкиДанных, ДанныеПроцесса, Отбор) Экспорт
	
	НастройкиКомпоновки = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	мдмОбщегоНазначенияКлиентСервер.ВосстановитьОтбор(НастройкиКомпоновки.Отбор, Отбор);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновки,,, Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, Новый Структура("ДанныеПроцесса", ДанныеПроцесса));
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	Таблица = Новый ТаблицаЗначений;
	
	ПроцессорВывода.УстановитьОбъект(Таблица);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);	
	
	Возврат Таблица.Количество() > 0;
	
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДобавитьПолеНабора(НаборДанных, Поле, ТипЗначения, ПутьКДанным, Заголовок)
	
	ПолеНабора = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
	ПолеНабора.Поле = Поле;
	ПолеНабора.ТипЗначения = ТипЗначения;
	ПолеНабора.ПутьКДанным = ПутьКДанным;
	ПолеНабора.Заголовок = ?(ЗначениеЗаполнено(Заголовок), Заголовок, Поле);
	
	Возврат ПолеНабора;
	
КонецФункции	

Функция ДобавитьПолеВыбора(Элементы, Имя, Заголовок)

	ВыбранноеПоле = Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПоле.Использование = Истина;
	ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных(Имя);
	ВыбранноеПоле.Заголовок = Заголовок;
	
	Возврат ВыбранноеПоле;
	
КонецФункции	

Функция ДобавитьГруппировкуДетальныеЗаписи(Структура)
	
	Группировка = Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	Группировка.Использование = Истина;
	
	ДетальныеЗаписи = Группировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
    ДетальныеЗаписи.Использование = Истина; 	
	
	Возврат Группировка;
	
КонецФункции	

#КонецОбласти