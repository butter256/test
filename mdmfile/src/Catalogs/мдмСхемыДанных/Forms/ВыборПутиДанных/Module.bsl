
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Параметры.Свойство("СхемаДанных", СхемаДанных);
	
	ЗаполнитьДанные();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьПутьКДанным();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДанныеФормыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьПутьКДанным();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Процедура ВыбратьПутьКДанным()
	
	ДанныеСтроки = Элементы.ДеревоАтрибутов.ТекущиеДанные;
	Если ДанныеСтроки = Неопределено тогда
		Возврат;
	КонецЕсли;	
	
	Результат = ДанныеСтроки.Имя;
	
	Родитель = ДанныеСтроки.ПолучитьРодителя();
	Если Родитель <> Неопределено тогда
		Результат = Родитель.Имя + "." + Результат;
	КонецЕсли;	
	
	ОповеститьОВыборе(Результат);
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьДанные()
	
	ДеревоАтрибутов.ПолучитьЭлементы().Очистить();
	
	МетаданныеОбъекта = мдмКэшируемыеФункции.МетаданныеОбъекта(СхемаДанных, мдмМетаданные.ВерсияМетаданных());	
	
	Для Каждого Реквизит из МетаданныеОбъекта.Реквизиты цикл
		
		СвойстваРеквизита = Реквизит.Значение;
		
		СтрокаРеквизит = ДеревоАтрибутов.ПолучитьЭлементы().Добавить();
		СтрокаРеквизит.Имя = СвойстваРеквизита.Имя;
		СтрокаРеквизит.Тип = СвойстваРеквизита.Тип;		
		СтрокаРеквизит.НомерКартинки = мдмКонструкторФормыКлиентСервер.ДанныеКартинкаРеквизит();
		
	КонецЦикла;	
	
	Для Каждого ТабличнаяЧасть из МетаданныеОбъекта.ТабличныеЧасти цикл
		
		СвойстваТабличнойЧасти = ТабличнаяЧасть.Значение;
		
		СтрокаТабличнаяЧасть = ДеревоАтрибутов.ПолучитьЭлементы().Добавить();
		СтрокаТабличнаяЧасть.Имя = СвойстваТабличнойЧасти.Имя;
		СтрокаТабличнаяЧасть.НомерКартинки = мдмКонструкторФормыКлиентСервер.ДанныеКартинкаТаблица();
		СтрокаТабличнаяЧасть.ЭтоТаблица = Истина;
		
		Для Каждого Реквизит из СвойстваТабличнойЧасти.Реквизиты цикл
			
			СвойстваРеквизита = Реквизит.Значение;
			
			СтрокаРеквизит = СтрокаТабличнаяЧасть.ПолучитьЭлементы().Добавить();
			СтрокаРеквизит.Имя = СвойстваРеквизита.Имя;
			СтрокаРеквизит.Тип = СвойстваРеквизита.Тип;
			СтрокаРеквизит.НомерКартинки = мдмКонструкторФормыКлиентСервер.ДанныеКартинкаРеквизит();
			
		КонецЦикла;	
		
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти