
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытийФормы

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	Если ВидФормы = "ФормаОбъекта" тогда
		
		Если Параметры.Свойство("Ключ") тогда
			
			СтандартнаяОбработка = Ложь;
			
			Параметры.Вставить("Ключ", Параметры.Ключ.Точка);
			
			ВыбраннаяФорма = Параметры.Ключ.Метаданные().Формы.ФормаЭлемента;
			
		КонецЕсли;	
		
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция ПолучитьПоИмени(Имя, БизнесПроцесс) Экспорт
	
	Возврат Справочники.мдмТочкиБизнесПроцесса.НайтиПоРеквизиту("Имя", Имя,, БизнесПроцесс);
	
КонецФункции

Функция СоздатьОбновитьТочки(Менеджер, Точка, Элемент, БизнесПроцесс) Экспорт
	
	Если ЗначениеЗаполнено(Точка) тогда
		Объект = Точка.ПолучитьОбъект();
	Иначе 
		Объект = Справочники.мдмТочкиБизнесПроцесса.СоздатьЭлемент();
		Объект.Владелец = БизнесПроцесс;
		Объект.УстановитьНовыйКод();
	КонецЕсли;	
	
	Объект.ПометкаУдаления = Ложь;
	Объект.Наименование = Элемент.Наименование;
	Объект.Имя = Элемент.Имя;
	Объект.ДополнительныеСвойства.Вставить("НеВыполнятьПроверкиЗаполнения");
	Объект.Записать();
	
	Точка = Объект.Ссылка;
	
	Возврат Объект.Ссылка;
	
КонецФункции	

Функция ОбновитьСвязиТочки(Менеджер, Точка, ТаблицаСвязей) Экспорт

	ТочкаОбъект = Точка.Точка.ПолучитьОбъект();
	
	Если Менеджер.ЕстьВходы() тогда
		ТочкаОбъект.Входы.Очистить();
	КонецЕсли;	
	Если Менеджер.ЕстьВыходы() тогда
		ТочкаОбъект.Выходы.Очистить();
	КонецЕсли;	
	
	Для Каждого Связь из ТаблицаСвязей цикл
		
		Если Связь.Начало = Точка.Точка.Имя И Менеджер.ЕстьВыходы() тогда
			
			НоваяСтрока = ТочкаОбъект.Выходы.Добавить();
			НоваяСтрока.Точка = Справочники.мдмТочкиБизнесПроцесса.ПолучитьПоИмени(Связь.Конец, Точка.Владелец);
			
		ИначеЕсли Связь.Конец = Точка.Точка.Имя И Менеджер.ЕстьВходы() тогда	
			
			НоваяСтрока = ТочкаОбъект.Входы.Добавить();
			НоваяСтрока.Точка = Справочники.мдмТочкиБизнесПроцесса.ПолучитьПоИмени(Связь.Начало, Точка.Владелец);
			
		КонецЕсли;	
		
	КонецЦикла;	
	
	ТочкаОбъект.ДополнительныеСвойства.Вставить("НеВыполнятьПроверкиЗаполнения");
	ТочкаОбъект.Записать();
	
	Возврат Точка;
	
КонецФункции	
	
#КонецОбласти

#КонецЕсли
