
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция КлючОбъекта() Экспорт
	Возврат "Имя,Владелец";
КонецФункции	

Процедура ВыборВарианта(ПроцессОбработкиОбъект, Результат) Экспорт
	
	Отказ = Ложь;
	
	ИмяСобытия = "ВыборВарианта";
	
	ВариантыТочки = Варианты();
	
	Контекст = Новый Структура;
	Контекст.Вставить("Объект", ПроцессОбработкиОбъект);
	Контекст.Вставить("Варианты", Новый ФиксированнаяСтруктура(ВариантыТочки));
	Контекст.Вставить("Результат", Результат);
	
	мдмТочкиБизнесПроцесса.ВыполнитьСобытие(Ссылка, ИмяСобытия, Контекст, Отказ);
	
	Если Не (Контекст.Свойство("Результат", Результат) И ТипЗнч(Результат) = Тип("Строка") И Варианты.Найти(Результат, "Имя") <> Неопределено) тогда
		Результат = Варианты[0].Имя;
	КонецЕсли;
	
	Если Отказ тогда
		ВызватьИсключение мдмТочкиБизнесПроцесса.ОписаниеОшибкиВыполненияСобытия(Ссылка, ИмяСобытия, "Не удалось выбрать вариант по процессу обработки " + ПроцессОбработкиОбъект.Ссылка); 
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ПриНачалеРаботыТочки(ПроцессОбработкиОбъект) Экспорт
	
	ПриЗавершенииРаботыТочки(ПроцессОбработкиОбъект);
	
КонецПроцедуры	

Процедура ПриЗавершенииРаботыТочки(ПроцессОбработкиОбъект) Экспорт
	
	Результат = "";
	
	ВыборВарианта(ПроцессОбработкиОбъект, Результат);
	
	Для Каждого СтрокаВыходы из Выходы Цикл 
		
		Если СтрокаВыходы.Результат = Результат тогда
			
			СтрокаВыходы.Точка.ПолучитьОбъект().ПриНачалеРаботыТочки(ПроцессОбработкиОбъект);
			
			Прервать;
			
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры	

Функция ПроверитьТочку() Экспорт
	
	Результат = Истина;
	
	Если Входы.Количество() = 0 Тогда
		Результат = Ложь;
		СообщитьОбОшибке("Не заполнены точки входа.");
	КонецЕсли;	
	
	Если Выходы.Количество() = 0 Тогда
		Результат = Ложь;
		СообщитьОбОшибке("Не заполнены точки выхода.");
	КонецЕсли;	
	
	Если Варианты.Количество() = 0 тогда
		Результат = Ложь;
		СообщитьОбОшибке("Не заполнены варианты.");
	КонецЕсли;	
	
	ВариантыДляПроверки = Варианты();
	Если Выходы.Количество() <> ВариантыДляПроверки.Количество() Тогда
		Результат = Ложь;
		СообщитьОбОшибке("Не для всех вариантов определены выходы.");
	Иначе
		Для Каждого СтрокаВыхода из Выходы Цикл 
			ВариантыДляПроверки.Удалить(СтрокаВыхода.Результат);
		КонецЦикла;	
		
		Если ВариантыДляПроверки.Количество() тогда
			Результат = Ложь;
			СообщитьОбОшибке("Не для всех вариантов определены выходы.");
		КонецЕсли;	
	КонецЕсли;
	
	Возврат Результат;
		
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Варианты()
	
	Результат = Новый Структура;
	
	Для Каждого СтрокаВарианта из Варианты Цикл 
		Результат.Вставить(СтрокаВарианта.Имя, СтрокаВарианта.Имя);
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции	

Процедура СообщитьОбОшибке(Сообщение)
	
	мдмОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Точка """ + Ссылка + """. " + Сообщение, Ссылка);
	
КонецПроцедуры	

#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли