
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция КлючОбъекта() Экспорт
	Возврат "Имя,Владелец";
КонецФункции	

Процедура ПроверкаУсловия(ПроцессОбработкиОбъект, Результат) Экспорт
	
	УсловиеПроверки = Условие.Отбор;
	
	Если ЗначениеЗаполнено(УсловиеПроверки) тогда
		
		Результат = мдмТочкиБизнесПроцесса.ВычислитьУсловие(УсловиеПроверки, ПроцессОбработкиОбъект);
		
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(ПроверкаУсловия) тогда
		
		Отказ = Ложь;
		
		ИмяСобытия = "ПроверкаУсловия";
		
		Контекст = Новый Структура;
		Контекст.Вставить("Объект", ПроцессОбработкиОбъект);
		Контекст.Вставить("Результат", Результат);
		
		мдмТочкиБизнесПроцесса.ВыполнитьСобытие(Ссылка, ИмяСобытия, Контекст, Отказ);
		
		Если Не (Контекст.Свойство("Результат", Результат) И ТипЗнч(Результат) = Тип("Булево")) тогда
			Результат = Ложь;
		КонецЕсли;
		
		Если Отказ тогда
			ВызватьИсключение мдмТочкиБизнесПроцесса.ОписаниеОшибкиВыполненияСобытия(Ссылка, ИмяСобытия, "Не удалось проверить условие выполнения по процессу обработки " + ПроцессОбработкиОбъект.Ссылка); 
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры	

Процедура ПриНачалеРаботыТочки(ПроцессОбработкиОбъект) Экспорт
	
	ПриЗавершенииРаботыТочки(ПроцессОбработкиОбъект);
	
КонецПроцедуры	

Процедура ПриЗавершенииРаботыТочки(ПроцессОбработкиОбъект) Экспорт

	Результат = Ложь;
	
	ПроверкаУсловия(ПроцессОбработкиОбъект, Результат);
	
	Для Каждого СтрокаВыходы из Выходы Цикл 
		
		Если СтрокаВыходы.Результат = Результат тогда
			
			СтрокаВыходы.Точка.ПолучитьОбъект().ПриНачалеРаботыТочки(ПроцессОбработкиОбъект);
			
			Прервать;
			
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры	

Функция ПроверитьТочку() Экспорт
	
	Результат = Истина;
	
	Если Входы.Количество() = 0 Тогда
		Результат = Ложь;
		СообщитьОбОшибке("Не заполнены точки входа.");
	КонецЕсли;	
	
	Если Выходы.Количество() = 0 Тогда
		Результат = Ложь;
		СообщитьОбОшибке("Не заполнены точки выхода.");
	ИначеЕсли Выходы.Количество() > 2 Тогда
		Результат = Ложь;
		СообщитьОбОшибке("Может быть только две точки выхода.");
	ИначеЕсли Выходы.Количество() = 2 Тогда
		КонтрольнаяПроверка = Ложь;
		Для Каждого СтрокаВыходы из Выходы Цикл
			КонтрольнаяПроверка = КонтрольнаяПроверка ИЛИ СтрокаВыходы.Результат;
		КонецЦикла;	
		Если Не КонтрольнаяПроверка тогда
			Результат = Ложь;
			СообщитьОбОшибке("Неверно настроены условия выхода. Должены быть условия Да и Нет.");
		КонецЕсли;	
	КонецЕсли;	
	
	Возврат Результат;
		
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СообщитьОбОшибке(Сообщение)
	
	мдмОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Точка """ + Ссылка + """. " + Сообщение, Ссылка);
	
КонецПроцедуры	

#КонецОбласти

#Иначе
  ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли