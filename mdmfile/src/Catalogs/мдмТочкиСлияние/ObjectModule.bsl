
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция КлючОбъекта() Экспорт
	Возврат "Имя,Владелец";
КонецФункции	

Процедура ПриНачалеРаботыТочки(ПроцессОбработкиОбъект) Экспорт
	
	ПриЗавершенииРаботыТочки(ПроцессОбработкиОбъект);
	
КонецПроцедуры	

Процедура ПриЗавершенииРаботыТочки(ПроцессОбработкиОбъект) Экспорт
	
	Если Не ВсеЗадачиВыполнены(ПроцессОбработкиОбъект.Ссылка) тогда
		
		Возврат;
		
	КонецЕсли;	
	
	Для Каждого СтрокаВыходы из Выходы Цикл 
		
		СтрокаВыходы.Точка.ПолучитьОбъект().ПриНачалеРаботыТочки(ПроцессОбработкиОбъект);
		
	КонецЦикла;	
	
КонецПроцедуры	

Функция ПроверитьТочку() Экспорт
	
	Результат = Истина;
	
	Если Входы.Количество() = 0 Тогда
		Результат = Ложь;
		СообщитьОбОшибке("Не заполнены точки входа.");
	КонецЕсли;	
	
	Если Выходы.Количество() = 0 Тогда
		Результат = Ложь;
		СообщитьОбОшибке("Не заполнены точки выхода.");
	ИначеЕсли Выходы.Количество() > 1 Тогда
		Результат = Ложь;
		СообщитьОбОшибке("Может быть только одна точка выхода.");
	КонецЕсли;	
	
	Возврат Результат;
		
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СообщитьОбОшибке(Сообщение)
	
	мдмОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Точка """ + Ссылка + """. " + Сообщение, Ссылка);
	
КонецПроцедуры	

Функция ВсеЗадачиВыполнены(БизнесПроцесс)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗадачаОбработки.Ссылка КАК Ссылка
		|ИЗ
		|	Задача.мдмЗадачаОбработки КАК ЗадачаОбработки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.мдмТочкиСлияние.Входы КАК ТочкиСлияниеВходы
		|		ПО ЗадачаОбработки.ТочкаБизнесПроцесса = ТочкиСлияниеВходы.Точка
		|			И (ТочкиСлияниеВходы.Ссылка = &Точка)
		|ГДЕ
		|	ЗадачаОбработки.БизнесПроцесс = &БизнесПроцесс
		|	И ЗадачаОбработки.Выполнена = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("БизнесПроцесс", БизнесПроцесс);
	Запрос.УстановитьПараметр("Точка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Пустой();
	
КонецФункции	

#КонецОбласти