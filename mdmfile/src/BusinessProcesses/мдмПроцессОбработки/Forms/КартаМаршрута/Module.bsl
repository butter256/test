
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ПроцессОбработки", ПроцессОбработки);
	
	Заголовок = "Карта маршрута " + ПроцессОбработки;
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаСервере()
	
	Карта = ПроцессОбработки.ПолучитьОбъект().ПолучитьКартуБизнесПроцесса();
	
КонецПроцедуры

&НаКлиенте
Процедура КартаВыбор(Элемент)
	
	ОбъектыЭлементаСхемы = ОбъектыЭлементаСхемы(Элемент.ТекущийЭлемент.Имя, ПроцессОбработки);	
	
	Если ОбъектыЭлементаСхемы.Количество() = 1 тогда	
		
		ПоказатьЗначение(, ОбъектыЭлементаСхемы[0]);
		
	ИначеЕсли ОбъектыЭлементаСхемы.Количество() > 1 тогда		
		
		Отбор = Новый Структура;
		Отбор.Вставить("Ссылка", ОбъектыЭлементаСхемы);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Отбор);
		
		ОткрытьФорму("Задача.мдмЗадачаОбработки.Форма.ФормаСписка", 
			ПараметрыФормы, 
			ЭтотОбъект,,,,, 
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбъектыЭлементаСхемы(Имя, ПроцессОбработки)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗадачаОбработки.Ссылка КАК Ссылка
		|ИЗ
		|	Задача.мдмЗадачаОбработки КАК ЗадачаОбработки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.мдмТочкиДействие КАК ТочкиДействие
		|		ПО ЗадачаОбработки.ТочкаБизнесПроцесса = ТочкиДействие.Ссылка
		|ГДЕ
		|	ТочкиДействие.Имя = &Имя
		|	И ТочкиДействие.Владелец = &БизнесПроцесс
		|	И ЗадачаОбработки.БизнесПроцесс = &ПроцессОбработки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПроцессОбработки.Ссылка
		|ИЗ
		|	Задача.мдмЗадачаОбработки КАК ЗадачаОбработки
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.мдмТочкиВложенныйПроцесс КАК ТочкиВложенныйПроцесс
		|		ПО ЗадачаОбработки.ТочкаБизнесПроцесса = ТочкиВложенныйПроцесс.Ссылка
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ БизнесПроцесс.мдмПроцессОбработки КАК ПроцессОбработки
		|		ПО ЗадачаОбработки.БизнесПроцесс = ПроцессОбработки.Ссылка
		|ГДЕ
		|	ТочкиВложенныйПроцесс.Имя = &Имя
		|	И ТочкиВложенныйПроцесс.Владелец = &БизнесПроцесс
		|	И ЗадачаОбработки.БизнесПроцесс = &ПроцессОбработки";
	
	Запрос.УстановитьПараметр("ПроцессОбработки", ПроцессОбработки);
	Запрос.УстановитьПараметр("БизнесПроцесс", ПроцессОбработки.БизнесПроцесс);
	Запрос.УстановитьПараметр("Имя", Имя);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции	