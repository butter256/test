
#Область ПрограммныйИнтерфейс

Функция ИдентификаторПоСтроке(Строка) Экспорт
	
	Результат = "";
	
	Символ = Лев(Строка, 1);
	Если СимволМежду(Символ, "а", "я") ИЛИ СимволМежду(Символ, "А", "Я") ИЛИ СимволМежду(Символ, "a", "z") ИЛИ СимволМежду(Символ, "A", "Z") тогда
		Результат = Результат + Символ;
	Иначе 
		Результат = Результат + "_";
	КонецЕсли;	
	
	Для инд = 2 по СтрДлина(Строка) цикл
		
		Символ = Сред(Строка, инд, 1);
		Если СимволМежду(Символ, "а", "я") ИЛИ СимволМежду(Символ, "А", "Я") ИЛИ СимволМежду(Символ, "a", "z") ИЛИ СимволМежду(Символ, "A", "Z") ИЛИ СимволМежду(Символ, "0", "9") тогда
			Результат = Результат + Символ;
		Иначе 
			Результат = Результат + "_";
		КонецЕсли;	
		
	КонецЦикла;	
	
	Пока СтрНайти(Результат, "__") Цикл
		Результат = СтрЗаменить(Результат, "__", "_");
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции	

Функция ЧислоМежду(Значение, От, До) Экспорт
	
	Возврат Значение >= От И Значение <= До; 
	
КонецФункции	

Функция СимволМежду(Значение, От, До) Экспорт 
	
	КодСимвола = КодСимвола(Значение);
	
	Возврат КодСимвола >= КодСимвола(От) И КодСимвола <= КодСимвола(До); 
	
КонецФункции	

Процедура СообщитьОбОшибке(Текст, КлючДанных = Неопределено) Экспорт
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = Текст;
	Сообщение.КлючДанных = КлючДанных;
	Сообщение.Сообщить();
	
КонецПроцедуры	

Процедура ВосстановитьОтбор(ОтборПриемник, ОтборИсточник) Экспорт
	
	ОтборПриемник.Элементы.Очистить();
	
	ВосстановитьОтборРекурсивно(ОтборПриемник, ОтборИсточник);
	
КонецПроцедуры	

Функция ЧислоСтрока(Число) Экспорт
	
	Возврат Формат(Число, "ЧГ=;");	
			
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВосстановитьОтборРекурсивно(ОтборПриемник, ОтборИсточник)
	
	Для Каждого Элемент из ОтборИсточник.Элементы цикл
		
		Если ТипЗнч(Элемент) = Тип("ЭлементОтбораКомпоновкиДанных") тогда
			НовыйЭлемент = ОтборПриемник.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(НовыйЭлемент, Элемент);
		ИначеЕсли ТипЗнч(Элемент) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") тогда 
			НоваяГруппа = ОтборПриемник.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ЗаполнитьЗначенияСвойств(НоваяГруппа, Элемент);
			ВосстановитьОтборРекурсивно(НоваяГруппа, Элемент);
		КонецЕсли;	
		
	КонецЦикла;	
	
КонецПроцедуры	

#КонецОбласти