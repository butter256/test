
#Область ПрограммныйИнтерфейс

Функция ПроверитьНаДублиПоРавенству(Источник, Знач Реквизиты) Экспорт
	
	Если ТипЗнч(Реквизиты) = Тип("Строка") тогда
	
		Реквизиты = СтрРазделить(Реквизиты, ",", Ложь);
	
	ИначеЕсли Не ТипЗнч(Реквизиты) = Тип("Структура") тогда
		
		ВызватьИсключение "Неверный тип значения ключа проверки на дубли.";	
		
	КонецЕсли;
	
	Если Реквизиты.Количество() = 0 тогда
		
		ВызватьИсключение "Не заданы реквизиты для проверки на дабли.";
		
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Ссылка КАК Ссылка
		|ИЗ
		|	" + Источник.Метаданные().ПолноеИмя() + " КАК Объект
		|ГДЕ
		|	Ссылка <> &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", Источник.Ссылка);
	
	Для Каждого Реквизит из Реквизиты цикл
		
		Если ТипЗнч(Реквизит) = Тип("Строка") тогда
		
			Запрос.Текст = Запрос.Текст + " И " + Реквизит + " = &" + Реквизит;
			
			Запрос.УстановитьПараметр(Реквизит, Источник[Реквизит]);
			
		ИначеЕсли ТипЗнч(Реквизит) = Тип("КлючИЗначение") тогда
			
			Запрос.Текст = Запрос.Текст + " И " + Реквизит.Ключ + " В (&" + Реквизит.Ключ + ")";
			
			Запрос.УстановитьПараметр(Реквизит.Ключ, Реквизит.Значение);
			
		КонецЕсли;
		
	КонецЦикла;	
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Результат = РезультатЗапроса.Пустой();
	
	Если Не Результат тогда
		мдмОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("Значение не уникально. Найдены дубли:");
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			мдмОбщегоНазначенияКлиентСервер.СообщитьОбОшибке("" + Выборка.Ссылка, Выборка.Ссылка);	
		КонецЦикла;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции	

Функция СериализоватьВXML(Значение) Экспорт
	
	Запись = Новый ЗаписьXML;
	Запись.УстановитьСтроку("UTF-8");
	
	СериализаторXDTO.ЗаписатьXML(Запись, Значение);
	
	Возврат Запись.Закрыть();
	
КонецФункции	

Функция ДесериализоватьИзXML(Строка) Экспорт
	
	Чтение = Новый ЧтениеXML;
	Чтение.УстановитьСтроку(Строка);
	
	Возврат СериализаторXDTO.ПрочитатьXML(Чтение);
	
КонецФункции	

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти