
#Область ПрограммныйИнтерфейс

Процедура ОбработатьСобытие(Форма, Элемент, ИмяСобытия, Параметры = Неопределено) Экспорт
	
	Перем СвойстваЭлемента, КодСобытия;
	
	Если Не Форма.ДобавленныеЭлементы.Свойство(Элемент, СвойстваЭлемента) ИЛИ СвойстваЭлемента = Неопределено тогда
		Возврат;
	КонецЕсли;	
	
	Если СвойстваЭлемента.События.Свойство(ИмяСобытия, КодСобытия)
		ИЛИ СвойстваЭлемента.ДополнительныеСобытия.Свойство(ИмяСобытия, КодСобытия) тогда
		
		Попытка
			
			ВыполнитьКодСобытия(ИмяСобытия, КодСобытия, Форма, Элемент, Параметры);
			
		Исключение
			
			ВызватьИсключение "При выполнении события """ + ИмяСобытия + """ элемента """ + Элемент + """ возникла ошибка: " + ОписаниеОшибки();
			
		КонецПопытки;	
		
	КонецЕсли;	
	
	Если ИмяСобытия = "ПриИзменении" И СвойстваЭлемента.ДополнительныеСобытия.Свойство("ПриИзмененииНаСервере") тогда
		
		Форма.ПриИзмененииНаСервере(Элемент);
		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ОбработатьКоманду(Форма, Команда, Контекст) Экспорт
	
	Перем СвойстваКоманды;
	
	Если Не Форма.ДобавленныеКоманды.Свойство(Команда, СвойстваКоманды) тогда
		Возврат;
	КонецЕсли;	
	
	Если Контекст = "Клиент" тогда
		КодКоманды = СвойстваКоманды.АлгоритмКлиент;
	Иначе 
		КодКоманды = СвойстваКоманды.АлгоритмСервер;	
	КонецЕсли;	
	
	Если ЗначениеЗаполнено(КодКоманды) тогда
		
		Попытка
			
			ВыполнитьКодКоманды(Команда, КодКоманды, Форма);
			
		Исключение
			
			ВызватьИсключение "При выполнении команды """ + Команда + """ возникла ошибка: " + ОписаниеОшибки();
			
		КонецПопытки;	
		
	КонецЕсли;	
	
	Если Контекст = "Клиент" И ЗначениеЗаполнено(СвойстваКоманды.АлгоритмСервер) тогда
		
		Форма.ДействиеКомандыНаСервере(Команда);	
				
	КонецЕсли;	
	
КонецПроцедуры	

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ВыполнитьКодКоманды(Команда, КодКоманды, Форма)
	
	Выполнить КодКоманды;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытия(Знач ИмяСобытия, Знач КодСобытия, Форма, Знач Элемент, Параметры)
	
	Если Элемент <> "Форма" тогда
		
		Элемент = Форма.Элементы[Элемент];
		
	КонецЕсли;	
	
	Если ИмяСобытия = "ПриИзменении" тогда
		
		ВыполнитьКодСобытияПриИзменении(КодСобытия, Форма, Элемент);	
		
	ИначеЕсли ИмяСобытия = "ПриИзмененииНаСервере" тогда
		
		ВыполнитьКодСобытияПриИзмененииНаСервере(КодСобытия, Форма, Элемент);	
		
	ИначеЕсли ИмяСобытия = "ДействиеНаСервере" тогда
		
		ВыполнитьКодСобытияДействиеНаСервере(КодСобытия, Форма, Параметры.Имя, Параметры.ВходныеПараметры, Параметры.ВыходныеПараметры)	
		
	ИначеЕсли ИмяСобытия = "НачалоВыбора" тогда
		
		ВыполнитьКодСобытияНачалоВыбора(КодСобытия, Форма, Элемент, Параметры.ДанныеВыбора, Параметры.СтандартнаяОбработка);	
		
	ИначеЕсли ИмяСобытия = "ОбработкаВыбора" тогда
		
		ВыполнитьКодСобытияОбработкаВыбора(КодСобытия, Форма, Элемент, Параметры.ВыбранноеЗначение, Параметры.СтандартнаяОбработка);	
		
	ИначеЕсли ИмяСобытия = "ПриНачалеРедактирования" тогда
		
		ВыполнитьКодСобытияПриНачалеРедактирования(КодСобытия, Форма, Элемент, Параметры.НоваяСтрока, Параметры.Копирование)
		
	ИначеЕсли ИмяСобытия = "ПриОкончанииРедактирования" тогда
		
		ВыполнитьКодСобытияПриОкончанииРедактирования(КодСобытия, Форма, Элемент, Параметры.НоваяСтрока, Параметры.ОтменаРедактирования)
		
	ИначеЕсли ИмяСобытия = "ПередУдалением" тогда
		
		ВыполнитьКодСобытияПередУдалением(КодСобытия, Форма, Элемент, Параметры.Отказ)
		
	ИначеЕсли ИмяСобытия = "ПослеУдаления" тогда
		
		ВыполнитьКодСобытияПослеУдаления(КодСобытия, Форма, Элемент)
		
	ИначеЕсли ИмяСобытия = "ПередНачаломДобавления" тогда
		
		ВыполнитьКодСобытияПередНачаломДобавления(КодСобытия, Форма, Элемент, Параметры.Отказ, Параметры.Копирование, Параметры.Родитель, Параметры.Группа, Параметры.Параметр)
		
	ИначеЕсли ИмяСобытия = "ПередНачаломИзменения" тогда
		
		ВыполнитьКодСобытияПередНачаломИзменения(КодСобытия, Форма, Элемент, Параметры.Отказ)
		
	ИначеЕсли ИмяСобытия = "ПриОбработкеОповещения" тогда
		
		ВыполнитьКодСобытияПриОбработкеОповещения(КодСобытия, Форма, Параметры.Результат, Параметры.ДополнительныеПараметры)
		
	ИначеЕсли ИмяСобытия = "ПередЗакрытием" тогда
		
		ВыполнитьКодСобытияПередЗакрытием(КодСобытия, Форма, Параметры.Отказ, Параметры.ЗавершениеРаботы, Параметры.ТекстПредупреждения, Параметры.СтандартнаяОбработка)
		
	ИначеЕсли ИмяСобытия = "ОбработкаПроверкиЗаполненияНаСервере" тогда
		
		ВыполнитьКодСобытияОбработкаПроверкиЗаполненияНаСервере(КодСобытия, Форма, Параметры.Отказ, Параметры.ПроверяемыеРеквизиты)
		
	ИначеЕсли ИмяСобытия = "ПередЗаписью" тогда
		
		ВыполнитьКодСобытияПередЗаписью(КодСобытия, Форма, Параметры.Отказ, Параметры.ПараметрыЗаписи)
		
	ИначеЕсли ИмяСобытия = "ПослеЗаписи" тогда
		
		ВыполнитьКодСобытияПослеЗаписи(КодСобытия, Форма, Параметры.ПараметрыЗаписи)
		
	ИначеЕсли ИмяСобытия = "ПередЗаписьюНаСервере" тогда	
		
		ВыполнитьКодСобытияПередЗаписьюНаСервере(КодСобытия, Форма, Параметры.Отказ, Параметры.ТекущийОбъект, Параметры.ПараметрыЗаписи)
		
	ИначеЕсли ИмяСобытия = "ПриСозданииНаСервере" тогда
		
		ВыполнитьКодСобытияПриСозданииНаСервере(КодСобытия, Форма, Параметры.Отказ, Параметры.СтандартнаяОбработка)
		
	ИначеЕсли ИмяСобытия = "ПриУсловномОформленииНаСервере" тогда
		
		ВыполнитьКодСобытияПриУсловномОформленииНаСервере(КодСобытия, Форма, Параметры.Элементы);
		
	Иначе
		
		ВызватьИсключение "Неизвестное событие " + ИмяСобытия; 
		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПриИзменении(Знач КодСобытия, Форма, Элемент)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПриИзмененииНаСервере(Знач КодСобытия, Форма, Элемент)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияНачалоВыбора(Знач КодСобытия, Форма, Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияОбработкаВыбора(Знач КодСобытия, Форма, Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПриНачалеРедактирования(Знач КодСобытия, Форма, Элемент, НоваяСтрока, Копирование)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПриОкончанииРедактирования(Знач КодСобытия, Форма, Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПередУдалением(Знач КодСобытия, Форма, Элемент, Отказ)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПослеУдаления(Знач КодСобытия, Форма, Элемент)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПередНачаломДобавления(Знач КодСобытия, Форма, Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПередНачаломИзменения(Знач КодСобытия, Форма, Элемент, Отказ)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПриОбработкеОповещения(Знач КодСобытия, Форма, Результат, ДополнительныеПараметры)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПередЗакрытием(Знач КодСобытия, Форма, Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПередЗаписью(Знач КодСобытия, Форма, Отказ, ПараметрыЗаписи)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПослеЗаписи(Знач КодСобытия, Форма, ПараметрыЗаписи)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияОбработкаПроверкиЗаполненияНаСервере(Знач КодСобытия, Форма, Отказ, ПроверяемыеРеквизиты)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияДействиеНаСервере(Знач КодСобытия, Форма, Имя, ВходныеПараметры, ВыходныеПараметры)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПриСозданииНаСервере(Знач КодСобытия, Форма, Отказ, СтандартнаяОбработка)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПередЗаписьюНаСервере(Знач КодСобытия, Форма, Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	

Процедура ВыполнитьКодСобытияПриУсловномОформленииНаСервере(Знач КодСобытия, Форма, Элементы)
	
	Выполнить КодСобытия;
	
КонецПроцедуры	


#КонецОбласти